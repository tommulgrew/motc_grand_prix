#include "menu"
#include "render"
#include "app"
#include "ui"
#include "tournament"

// Constants

// Main menu item enums

const int MAIN_MENU_TRACK = 1;
const int MAIN_MENU_LAPS = 2;
const int MAIN_MENU_DIFFICULTY = 3;
const int MAIN_MENU_STARTRACE = 4;
const int MAIN_MENU_QUIT = 5;
const int MAIN_MENU_RACETYPE = 6;
const int MAIN_MENU_TOURNAMENT_NEW = 7;
const int MAIN_MENU_TOURNAMENT_QUIT = 8;

string[] raceTypeNames = new string[] {
    "Single Race",
    "Championship",
    "Practice"
};

// Structures

struct MainMenuState {
    MenuState state;
    int carPaletteIndex;
    int idleTimer;
}

// Data

MenuParams mainMenuParams = new MenuParams {
    y = SCREEN_HEIGHT * 0.8,
    itemSpacing = 14,
    barHeight = 16,
    drawBackground = false
};
MainMenuState mainMenu;

// Routines

MenuItem[] getMainMenuItems();
void startRace(RaceParams raceParams);
void doDemo();
void drawTournamentBoard();

void initMainMenu() {
    mainMenu = new MainMenuState {
        state = new MenuState,
        carPaletteIndex = rnd() * aiCarPalettes.length
    };
}

void drawMainMenu() {
    cls(1);

    if (gameConfig.raceType = RACETYPE_TOURNAMENT) {
        drawTournamentBoard();
    }
    else {

        // Title text
        const int TITLE_BASE_SCALE = 2;
        const int TITLE_X = SCREEN_WIDTH / 2 - 8 * 6 * TITLE_BASE_SCALE * PRATIO;
        const int TITLE_Y = SCREEN_HEIGHT * 0.15;
        const int TITLE_SCALE = TITLE_BASE_SCALE * PRATIO;
        color(5);
        printScaled("Mot's Grand Prix", TITLE_X, TITLE_Y + 2 * PRATIO, TITLE_SCALE);
        color(6);
        printScaled("Mot's Grand Prix", TITLE_X, TITLE_Y, TITLE_SCALE);

        // Draw a racing car
        colmap(aiCarPalettes[mainMenu.carPaletteIndex]);
        drawCar(new Transform {
            translate = new Vec { z = 2.5, y = -0.1 },
            skewX = -0.5,
            skewY = -0.3
        });
        colmap();
    }

    // Menu UI
    MenuItem[] items = getMainMenuItems();
    drawMenu(mainMenuParams, mainMenu.state, items);
}

void updateMainMenu(UIInput input) {

    // Items
    MenuItem[] items = getMainMenuItems();

    // Update menu
    MenuAction action = updateMenu(mainMenu.state, items, input);

    // Perform menu action
    if (action != null) {
        if (action.menuAction = MENU_ACTION_SELECT) {
            if (action.itemID = MAIN_MENU_STARTRACE) {

                // Common race params
                RaceParams raceParams = new RaceParams {
                    laps = lapOptions[gameConfig.lapOptionIndex],
                    difficulty = difficultyLevels[gameConfig.difficulty],
                    aiCarRoster = new int[0],
                    isPracticeMode = gameConfig.raceType = RACETYPE_PRACTICE,
                    playerColour = gameConfig.playerColour
                };

                if (gameConfig.raceType = RACETYPE_TOURNAMENT) {

                    // Tournament params
                    raceParams.track = tracks[tournament.trackOrder[tournament.raceIndex]];
                    for (TournamentCar car : tournament.cars) {
                        if (car.carIndex != -1) {           // Not player
                            raceParams.aiCarRoster.add(car.carIndex);
                        }
                    }                
                }
                else {

                    // Single race/practice params
                    raceParams.track = tracks[gameConfig.trackIndex];
                    if (gameConfig.raceType = RACETYPE_SINGLE) {
                        raceParams.aiCarRoster = makeAICarRoster(AI_CAR_COUNT);
                    }
                }
                startRace(raceParams);
            }
            else if (action.itemID = MAIN_MENU_TOURNAMENT_NEW) {
                newTournament();
            }
            else if (action.itemID = MAIN_MENU_TOURNAMENT_QUIT) {
                quitTournament();
            }
            else if (action.itemID = MAIN_MENU_QUIT) {
                saveConfig();
                saveTournament();
                quitApp = true;
            }
        }
        else if (action.menuAction = MENU_ACTION_CHANGE) {
            if (action.itemID = MAIN_MENU_RACETYPE) {
                gameConfig.raceType = clamp(gameConfig.raceType + action.delta, 0, 2);
            }
            if (action.itemID = MAIN_MENU_TRACK) {
                gameConfig.trackIndex = clamp(gameConfig.trackIndex + action.delta, 0, tracks.length - 1);
            }
            else if (action.itemID = MAIN_MENU_LAPS) {
                gameConfig.lapOptionIndex = clamp(gameConfig.lapOptionIndex + action.delta, 0, lapOptions.length - 1);
            }
            else if (action.itemID = MAIN_MENU_DIFFICULTY) {
                gameConfig.difficulty = clamp(gameConfig.difficulty + action.delta, 0, difficultyLevels.length - 1);
            }
        }
    }

    // Run demo after X seconds of idle time
    mainMenu.idleTimer++;
    for (bool btnDown : input.btnDown) {
        if (btnDown) {
            mainMenu.idleTimer = 0;
            break;
        }
    }
    if (mainMenu.idleTimer > 30 * 30) {
        doDemo();
    }
}

MenuItem[] getMainMenuItems() {
    MenuItem[] items = new MenuItem[] {
        new MenuItem { id = MAIN_MENU_RACETYPE, text = raceTypeNames[gameConfig.raceType] }
    };
    if (gameConfig.raceType != RACETYPE_TOURNAMENT) {
        items.add(new MenuItem { id = MAIN_MENU_TRACK, text = "Track: " + tracks[gameConfig.trackIndex].name });
    }
    if (gameConfig.raceType != RACETYPE_PRACTICE) {
        items.add(new MenuItem { id = MAIN_MENU_LAPS, text = "Laps: " + lapOptions[gameConfig.lapOptionIndex] });
        items.add(new MenuItem { id = MAIN_MENU_DIFFICULTY, text = "Difficulty: " + difficultyLevels[gameConfig.difficulty].name });
    }
    if (gameConfig.raceType = RACETYPE_TOURNAMENT) {
        if (not tournament.isActive) {
            items.add(new MenuItem { id = MAIN_MENU_TOURNAMENT_NEW, text = "New Championship" });
        }
        else {
            items.add(new MenuItem { id = MAIN_MENU_TOURNAMENT_QUIT, text = "Quit Championship", confirm = true });
            items.add(new MenuItem { id = MAIN_MENU_STARTRACE, text = "!! Race !!" });    
        }
    }
    else {
        items.add(new MenuItem { id = MAIN_MENU_STARTRACE, text = "!! Race !!" });    
    }

    // Exclude quit option from web version, as quitting doesn't make sense.
    // (The game just ends and the application becomes unresponsive.)
    if (hostplatform() != "web") {
        items.add(new MenuItem { id = MAIN_MENU_QUIT, text = "Quit game" });    
    }

    return items;
}

void drawTournamentBoard() {
    const int BOARD_WIDTH = 200 * PRATIO;
    const int BOARD_HEIGHT = 110 * PRATIO;
    const int BOARD_X = SCREEN_WIDTH / 2 - BOARD_WIDTH / 2;
    const int BOARD_Y = 10 * PRATIO;
    color(0);
    rectfill(BOARD_X, BOARD_Y, BOARD_X + BOARD_WIDTH, BOARD_Y + BOARD_HEIGHT);
    color(6);
    rect(BOARD_X, BOARD_Y, BOARD_X + BOARD_WIDTH - 1, BOARD_Y + BOARD_HEIGHT - 1);
    color(5);
    rect(BOARD_X + 1, BOARD_Y + 1, BOARD_X + BOARD_WIDTH, BOARD_Y + BOARD_HEIGHT);

    const int CAR_X = BOARD_X + 10 * PRATIO;
    const int CAR_Y = BOARD_Y + 15 * PRATIO;
    const int CAR_SPACING = 10 * PRATIO;
    const int HEADING_Y = CAR_Y - 10 * PRATIO;
    const int POINTS_X = BOARD_X + BOARD_WIDTH - 45 * PRATIO;
    print("Driver", CAR_X, HEADING_Y);
    print("Points", POINTS_X, HEADING_Y);

    if (tournament.isActive) {

        // Car names and points
        for (int i = 0; i < tournament.cars.length; i++) {
            TournamentCar car = tournament.cars[i];
            int y = CAR_Y + i * CAR_SPACING;
            color(car.carIndex >= 0 ? 6 : 7);
            drawCarName(
                car.carIndex >= 0 ? aiCarPalettes[car.carIndex] : null,
                car.carIndex >= 0 ? aiCarNames[car.carIndex] : "Player",
                CAR_X, y);
            print(formatInt(car.points, 6, " "), POINTS_X, y + 2 * PRATIO);
        }

        // Next track
        const int TRACK_X = BOARD_X + 4 * PRATIO;
        const int TRACK_Y = BOARD_Y + BOARD_HEIGHT - 11 * PRATIO;
        color(7);
        if (tournament.raceIndex < tournament.trackOrder.length) {
            print("Next: " + tracks[tournament.trackOrder[tournament.raceIndex]].name, TRACK_X, TRACK_Y);
        }
        else {
            print("Championship Finished!", TRACK_X, TRACK_Y);
        }
    }
}

void startRace(RaceParams raceParams) {

    // Save settings
    saveConfig();
    saveTournament();

    // Set race params
    race = raceParams;

    // Switch to race mode
    setGameMode(GAMEMODE_RACE);
}

void doDemo() { 
    startRace(new RaceParams {
        track = tracks[rnd() * tracks.length],
        laps = 1,
        difficulty = difficultyLevels[2],
        aiCarRoster = makeAICarRoster(AI_CAR_COUNT),
        playerColour = rnd() * playerCarPalettes.length,
        isDemo = true
    });
}
