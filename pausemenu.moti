#include "menu"
#include "render"
#include "gameconfig"

// Constants

// Pause menu item enums

const int PAUSE_RESUME = 1;
const int PAUSE_QUIT = 2;

// Structures

struct PauseMenuState {
    bool isActive;
    MenuState menuState;
}

// Data

MenuParams pauseMenuParams = new MenuParams {
    y = SCREEN_HEIGHT / 2,
    itemSpacing = 30,
    drawBackground = true,
    backgroundColour = 1,
    borderColour = 7,
    backgroundPadding = 15
};
PauseMenuState pauseMenuState = new PauseMenuState { 
    menuState = new MenuState 
};

// Routines

MenuItem[] getPauseMenuItems();
void pauseMenuAction(MenuAction action);

void updatePauseMenu(UIInput input) {
    if (pauseMenuState.isActive) {

        // Items
        MenuItem[] items = getPauseMenuItems();

        // Update menu
        MenuAction action = updateMenu(pauseMenuState.menuState, items, input);

        // Perform menu action
        if (action != null) {
            if (action.menuAction = MENU_ACTION_SELECT) {
                if (action.itemID = PAUSE_QUIT) {
                    pauseMenuState.isActive = false;
                    setGameMode(GAMEMODE_MENU);
                }
                else if (action.itemID = PAUSE_RESUME) {
                    pauseMenuState.isActive = false;
                }
            }
        }

        // Pressing pause again exits the pause menu
        if (input.btnPressed[UI_PAUSE]) {
            pauseMenuState.isActive = false;
        }
    }
    else {
        // Pressing pause displays pause menu
        if (input.btnPressed[UI_PAUSE]) {
            pauseMenuState.isActive = true;
            pauseMenuState.menuState = new MenuState;
        }
    }
}

void drawPauseMenu() {
    MenuItem[] items = getPauseMenuItems();
    drawMenu(pauseMenuParams, pauseMenuState.menuState, items);
}

MenuItem[] getPauseMenuItems() {
    MenuItem[] items = new MenuItem[0];
    items.add(new MenuItem { id = PAUSE_QUIT, text = "Quit race" });
    items.add(new MenuItem { id = PAUSE_RESUME, text = "Resume" });
    return items;
}

void pauseMenuAction(MenuAction action) {
    if (action.menuAction = MENU_ACTION_SELECT) {
        if (action.itemID = PAUSE_QUIT) {
            pauseMenuState.isActive = false;
            setGameMode(GAMEMODE_MENU);
        }
        else if (action.itemID = PAUSE_RESUME) {
            pauseMenuState.isActive = false;
        }
    }
}
